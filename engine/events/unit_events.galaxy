//=========================================================
//		Apex Roleplaying Engine
//		Copyright (c) 2022, Apex Group
//=========================================================
// This software is distributed under the MIT License:
// <http://opensource.org/licenses/MIT>
//=========================================================

// LibapxUnitDeathCleanup
// =======================
// whenever unit is killed or is removed, must cleanup unit information:
// -> clean data: must remove unit's cache from the data table
//    (to conserve data table memory usage)
//    along with custom name/name color
// -> if unit has region waygate dialog attached, must anchor the waygate
//    dialog to position where unit died
// TODO: ALL new Unit info
//Desc, name, subtitle, subtitle border/bg colors, setname, unitcard, log, keypress
bool LibapxUnitDeathCleanup(bool testConds, bool runActions) {
	string rgnName;
	int i;
	unit u = EventUnit();
	int waygateDialog = FixedToInt( UnitGetCustomValue(u, gv_LibapxUnitIndexWaygateDialog) );
	string unitId = FixedToString(UnitGetCustomValue(u, 0), 0); // 0 is index for unit id
	int owner = UnitGetOwner(u);
	// remove data table data (see unit.galaxy)
	LibapxUnitDataRemove(u);
	LibapxClearAttachAndRotationPreviousCaches(u);

	//Remove unit variables
	LibapxTManagerCleanup(gv_LibapxUnitVariableMapPrefix + unitId);
	//If matches current selection
	if(UnitGroupUnit(UnitGroupSelected(owner), 1) == u) {
		LibapxVariablesUiRefresh(owner);
	}

	// if waygate dialog exists, anchor to position where unit died
	if( waygateDialog > 0 ) {
		rgnName = DataTableGetString(true, gv_LibapxWaygateDialogRegDataTableIdentifier + " " + IntToString(waygateDialog));
		// anchor dialog to position where unit died with a new unit
		UnitCreate(1, LibapxWaygateDialogAnchorUnit, c_unitCreateIgnorePlacement, 0, UnitGetPosition(u), 270.0);
		DataTableSetUnit(true, gv_LibapxWaygateAnchorDataTableIdentifier + " " + rgnName, UnitLastCreated());

		//ActorSend(libNtve_gf_MainActorofUnit(UnitLastCreated()), "SetOpacity {0.1}"); // set opacity to hide
		// hide the beacons for all players
		for( i = 1; i < gv_LibapxMaxPlayers; i += 1 ) {
			ActorSend(libNtve_gf_MainActorofUnit(UnitLastCreated()), "SetFilterPlayers " + IntToString(i));
		}
		UnitSetCustomValue(UnitLastCreated(), gv_LibapxUnitIndexWaygateAnchor, 1.0);

		DialogSetPositionRelativeToUnit(waygateDialog, UnitLastCreated(), "RefOverhead", -60, 0);
	}

	return true;
}
